// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/05/CPU.hdl

/**
 * The Hack CPU (Central Processing unit), consisting of an ALU,
 * two registers named A and D, and a program counter named PC.
 * The CPU is designed to fetch and execute instructions written in 
 * the Hack machine language. In particular, functions as follows:
 * Executes the inputted instruction according to the Hack machine 
 * language specification. The D and A in the language specification
 * refer to CPU-resident registers, while M refers to the external
 * memory location addressed by A, i.e. to Memory[A]. The inM input 
 * holds the value of this location. If the current instruction needs 
 * to write a value to M, the value is placed in outM, the address 
 * of the target location is placed in the addressM output, and the 
 * writeM control bit is asserted. (When writeM==0, any value may 
 * appear in outM). The outM and writeM outputs are combinational: 
 * they are affected instantaneously by the execution of the current 
 * instruction. The addressM and pc outputs are clocked: although they 
 * are affected by the execution of the current instruction, they commit 
 * to their new values only in the next time step. If reset==1 then the 
 * CPU jumps to address 0 (i.e. pc is set to 0 in next time step) rather 
 * than to the address resulting from executing the current instruction. 
 */

CHIP CPU {

    IN  inM[16],         // M value input  (M = contents of RAM[A])
        instruction[16], // Instruction for execution
        reset;           // Signals whether to re-start the current
                         // program (reset==1) or continue executing
                         // the current program (reset==0).

    OUT outM[16],        // M value output
        writeM,          // Write to M? 
        addressM[15],    // Address in data memory (of M)
        pc[15];          // address of next instruction

    PARTS:
    Not(in=instruction[15], out=Ainstruction);
    Not(in=Ainstruction, out=Cinstruction);

    // Put your code here:
    And(a=Cinstruction, b=instruction[5], out=ALUtoA);  
    Mux16(a=instruction, b=outAlu, sel=ALUtoA, out=preARegister);
    //We store in Register a when we have and A instruction (instruction[15] is 0)
    //or C instruction with instruction[5] is 1
    Or(a=Ainstruction, b=ALUtoA, out=loadA);
    ARegister(in=preARegister, load=loadA, out=outARegister, out[0..14]=addressM);
    
    // In case of C instruction a byte determines wether ALU will be fed from A or incoming M
    And(a=Cinstruction, b=instruction[12], out=aBitFlag);
    Mux16(a=outARegister, b=inM, sel=aBitFlag, out=yAluIn);
    
    // In case of a C (instruction[15] is 1) instruction and instruction[4] is set) we store aluOut in d
    And(a=Cinstruction, b=instruction[4], out=loadD);
    DRegister(in=outAlu, load=loadD, out=xAluIn);
    
    ALU(x=xAluIn, y=yAluIn, zx=instruction[11], nx=instruction[10], 
        zy=instruction[9], ny=instruction[8], f=instruction[7],
        no=instruction[6], out=outAlu, out=outM, zr=zr, ng=ng); // calculate
        
    // We have C instruction, and M bit in dest is set)
    And(a=Cinstruction, b=instruction[3], out=writeM);
    
    
    And(a=zr, b=instruction[1], out=jeq);    // is zero and jump if zero
    And(a=ng, b=instruction[2], out=jlt);    // is neg and jump if neg
    Or(a=zr, b=ng, out=zeroOrNeg);
    Not(in=zeroOrNeg, out=positive);            // is positive (not zero and not neg)
    And(a=positive, b=instruction[0], out=jgt); // is pos and jump if pos
    Or(a=jeq, b=jlt, out=jle);
    Or(a=jle, b=jgt, out=jumpToA);              // load PC if cond met and jump if cond
    And(a=Cinstruction, b=jumpToA, out=PCload); // Only jump if C instruction
    Not(in=PCload, out=PCinc);                  // only inc if not load
    PC(in=outARegister, inc=PCinc, load=PCload, reset=reset, out[0..14]=pc);
}
